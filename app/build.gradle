apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

ext.readBuildNumber = {
    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        def versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        return code
    } else {
        throw new GradleException("Could not read version.properties!")
    }
}

ext {
    versionMajor = "0"
    versionMid = "7"
    versionMinor = "4"

    customBuildNumber = readBuildNumber()
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.neoproduction.gasolinni"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode customBuildNumber
        versionName "${versionMajor}.${versionMid}.${versionMinor}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def appName = "gasolinni"
            outputFileName = appName + "_v${variant.versionName}.${variant.versionCode}.apk"
        }
    }
}

dependencies {
    def room_version = "2.2.4"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Material, ViewPager 2
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.google.android.material:material:1.1.0'

    // Room + Coroutines support
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.4'

    // Kotlin KTX
    implementation "androidx.fragment:fragment-ktx:1.2.2"
    implementation "androidx.activity:activity-ktx:1.1.0"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'

    // Firebase
    implementation 'com.google.firebase:firebase-database-ktx:19.2.1'
    implementation 'com.google.firebase:firebase-auth:19.2.0'

    // Map API
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:9.0.0'
}
